.386
.model flat,STDCALL

 extrn LoadLibraryA:Proc, GetProcAddress:Proc, GlobalAlloc:Proc, GlobalLock:Proc
 extrn GlobalUnlock:Proc, GetDeviceCaps:Proc, GetClientRect:Proc, ReadFile:Proc
 extrn CreateFileA:Proc, GetFileSize:Proc, CloseHandle:Proc, MulDiv:Proc
 extrn GetDC:Proc, ReleaseDC:Proc, ExitProcess: Proc

.data
  HWnd		dd	0
  HDC		dd	0

HMWidth		dd	0
Width		dd	0
HMHeight	dd	0
Height		dd	0
RECT	dd	0,0,0,0		;left,top,right,bottom

PICTURE_NAME	db 'test.jpg',0

HFile		dd	0
File_Size	dd	0
HGlobal		dd	0
NUM_READ	dd	0
PSTM		dd	0
	; iдентифiкатор iнтерфейсу IPicture, який взятий з реєстру:
IID_IPicture	dd	7bf80980h		; HKEY_CLASSES_ROOT\Interface\
		dw	0bf32h, 101ah		; {7BF80980-BF32-101A-8BBB-00AA00300CAB}
		db	8bh, 0bbh
		db	00h, 0aah, 00h, 30h, 0ch, 0abh

GPICTURE	dd	0

LibName1		db 'ole32.dll',0
ProcName1		db 'CreateStreamOnHGlobal',0
CreateStreamOnHGlobal1	dd  offset STOP
LibName2		db 'oleaut32.dll',0
ProcName2		db 'OleLoadPicture',0
OleLoadPicture1		dd  offset STOP

.code
Start:	call LoadLibraryA,offset LibName1
	call GetProcAddress, eax, offset ProcName1
	or eax,eax
	jz STOP
	mov CreateStreamOnHGlobal1,eax

	call LoadLibraryA,offset LibName2
	call GetProcAddress, eax, offset ProcName2
	or eax,eax
	jz STOP
	mov OleLoadPicture1,eax

	call CreateFileA, offset PICTURE_NAME, 80000000h,0,0,3,0,0
	cmp eax,-1
	jz STOP
	mov HFile,eax

	call GetFileSize,HFile,0	; отримати довжину файлу
	mov  File_Size,eax
	call GlobalAlloc, 2, File_Size
	mov  HGlobal,eax
	call GlobalLock,eax
	call ReadFile, HFile, eax, File_Size, offset NUM_READ,0
	call GlobalUnlock, HGlobal
	call CloseHandle, HFile

	call [CreateStreamOnHGlobal1], HGlobal, 1, offset PSTM
	mov eax,PSTM	; завантажити файл у потiк
	or eax,eax
	jz STOP

	call [OleLoadPicture1],PSTM,File_Size,0,offset IID_IPicture,offset GPICTURE
	or eax,eax	; скористатись інтерфейсом об'єкт IPicture
	jnz STOP

	mov edx,PSTM	; coinvoke PSTM,IUnknown,Release
	mov edx,[edx]
	push PSTM
	call dword ptr [edx+8h]

	mov edx, GPICTURE	; використання методу OLE
	mov edx, [edx]		; IPicture::get_Width (18h)
	push offset HMWidth
	push GPICTURE
	call Dword ptr [edx+18h]

	mov edx, GPICTURE	; використання методу OLE
	mov edx, [edx]		; IPicture::get_Height (1Ch)
	push offset HMHeight
	push GPICTURE
	call Dword ptr [edx+1Ch]

	call GetDC,HWnd
	mov  HDC,eax
	call GetDeviceCaps, HDC,88	; LOGPIXELSX
	call MulDiv, HMWidth,eax,254*8	;9ECh	; HIMETRIC_INCH
	mov  Width, eax
	call GetDeviceCaps, HDC,90	; LOGPIXELSY
	call MulDiv, HMHeight,eax,254*8	;9ECh	; HIMETRIC_INCH
	mov  Height, eax
	call GetClientRect, HWnd, offset RECT

	mov edx, GPICTURE	; використання методу OLE
	mov edx, [edx]		; IPicture::Render (20h)
	mov eax, HMHeight
	neg eax
	call [edx+20h],GPICTURE,HDC,0,0,Width,Height,0,HMHeight,HMWidth,eax,offset RECT

	call ReleaseDC,HWnd,HDC
STOP:	call ExitProcess,0
end Start
